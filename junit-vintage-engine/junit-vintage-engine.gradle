description = 'JUnit Vintage Engine'

test {
	useJUnitPlatform {
		includeEngines 'junit-jupiter'
	}
	filter {
		includeTestsMatching '*Tests'
	}
	systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
}

dependencies {
	api(project(':junit-platform-engine'))
	implementation("junit:junit:${junit4Version}")

	testImplementation(project(':junit-platform-launcher'))
	testImplementation(project(':junit-jupiter-api'))
	testImplementation(project(':junit-platform-runner'))
	testImplementation(project(path: ':junit-jupiter-engine', configuration: 'testArtifacts'))
	testImplementation(project(path: ':junit-platform-engine', configuration: 'testArtifacts'))
	testImplementation("org.assertj:assertj-core:${assertJVersion}")
	testImplementation("org.mockito:mockito-core:${mockitoVersion}")

	// Include junit-platform-console so that the JUnit Gradle plugin
	// uses the local version of the ConsoleLauncher.
	testRuntimeOnly(project(':junit-platform-console'))

	testRuntimeOnly(project(':junit-jupiter-engine'))
	testRuntimeOnly("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	testRuntimeOnly("org.apache.logging.log4j:log4j-jul:${log4jVersion}")
}

jar {
	manifest {
		attributes(
			'Automatic-Module-Name': 'org.junit.vintage.engine'
		)
	}
}
