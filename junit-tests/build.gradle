apply plugin: 'jacoco'
apply plugin: 'org.junit.gen5.gradle'

uploadArchives.enabled = false

junit5 {
	//version '5.0.0-SNAPSHOT' //not needed since all dependencies are given explicitly
	runJunit4 false //Does not work yet in master
	matchClassName '.*Test[s]'
	// includeTag 'fast'
	// excludeTag 'slow'
}

def testedProjects = [
	project(':junit-commons'),
	project(':junit-console'),
	project(':junit-engine-api'),
	project(':junit-launcher'),
	project(':junit4-launcher-runner'),
	project(':junit4-engine'),
	project(':junit5-api'),
	project(':junit5-engine')
]

dependencies {
	testedProjects.each { project ->
		testCompile(project)
	}

	testCompile("org.assertj:assertj-core:${assertJVersion}")
	testCompile("junit:junit:${junit4Version}")
	testCompile("org.mockito:mockito-core:${mockitoVersion}")
	testCompile("de.schauderhaft.degraph:degraph-check:${degraphVersion}")

	testRuntime("org.apache.logging.log4j:log4j-core:${log4JVersion}")
	testRuntime("org.apache.logging.log4j:log4j-jul:${log4JVersion}")
}

jacoco {
	toolVersion = '0.7.4.201502262128'
}

jacocoTestReport {
	additionalSourceDirs = files()
	additionalClassDirs = files()
	testedProjects.each { project ->
		additionalSourceDirs = files(additionalSourceDirs, project.sourceSets.main.allSource.srcDirs)
		additionalClassDirs = files(additionalClassDirs, project.sourceSets.main.output)
	}
	reports {
		xml.enabled = true
		html.enabled = true
	}
}
